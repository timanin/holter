apply plugin: 'java'
apply plugin: 'gradle-one-jar'
apply plugin: 'maven'
apply plugin: 'distribution'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

group = 'com.sergeytimanin'
version = '0.4'

buildscript {
	repositories { mavenCentral() }

	dependencies { classpath 'com.github.rholder:gradle-one-jar:1.0.4' }
}

project.ext {
	dropwizardVersion = '0.8.0-rc2'
    junitVersion = '4.11'
	mainClassName = 'com.sergeytimanin.holter.HolterApplication'
}

repositories { mavenCentral() }

dependencies {
	compile group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
	testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
    testCompile "org.mockito:mockito-all:1.9.5"
    testCompile "org.easytesting:fest-assert-core:2.0M10"
//    testCompile group: 'com.sun.jersey', name: 'jersey-client', version: '1.8'
}

task fatJar(type: OneJar) { mainClass = mainClassName }

task wrapper(type: Wrapper) { gradleVersion = '1.12' }

def configFile = file('src/dist/settings.yml')
def scriptFile = file('src/dist/start.sh')

distributions {
	main {
		contents {
			into('bin') {
				from scriptFile
				from fatJar.outputs
				fileMode = 0755
			}
			into('conf') { from configFile }
		}
	}
}
